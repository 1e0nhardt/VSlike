extends CanvasLayer

signal back_pressed

@onready var window_button: Button = %WindowButton
@onready var back_button: Button = %BackButton
@onready var sfx_slider: HSlider = %SfxSlider
@onready var music_slider: HSlider = %MusicSlider


func _ready():
    window_button.pressed.connect(on_window_button_pressed)
    back_button.pressed.connect(on_back_button_pressed)
    sfx_slider.value_changed.connect(on_slider_value_changed.bind("sfx"))
    music_slider.value_changed.connect(on_slider_value_changed.bind("music"))
    update_display()


func update_display():
    var mode = DisplayServer.window_get_mode()

    if mode != DisplayServer.WINDOW_MODE_FULLSCREEN:
        window_button.text = "Fullscreen"
    else:
        window_button.text = "Windowed"
    
    sfx_slider.value = get_bus_volume_percent("sfx")
    music_slider.value = get_bus_volume_percent("music")


func get_bus_volume_percent(bus_name: String):
    var bus_index = AudioServer.get_bus_index(bus_name)
    var volume_db = AudioServer.get_bus_volume_db(bus_index)
    return db_to_linear(volume_db)


func set_bus_volume_percent(bus_name: String, percent: float):
    var bus_index = AudioServer.get_bus_index(bus_name)
    var volume_db = linear_to_db(percent)
    AudioServer.set_bus_volume_db(bus_index, volume_db)


func on_window_button_pressed():
    var mode = DisplayServer.window_get_mode()

    if mode != DisplayServer.WINDOW_MODE_FULLSCREEN:
        DisplayServer.window_set_mode(DisplayServer.WINDOW_MODE_FULLSCREEN)
    else:
        DisplayServer.window_set_mode(DisplayServer.WINDOW_MODE_WINDOWED)
    
    update_display()


func on_slider_value_changed(value: float, bus_name: String):
    set_bus_volume_percent(bus_name, value)


func on_back_button_pressed():
    back_pressed.emit()